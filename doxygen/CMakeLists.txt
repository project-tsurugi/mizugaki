if (NOT Doxygen_FOUND)
    message(FATAL_ERROR "doxygen was not installed, please configure with -DBUILD_DOCUMENTS=OFF to skip doxygen")
endif()

set(TARGET_INCLUDES "")
foreach (target_name
        mizugaki-api)
    get_target_property(
        _includes
        ${target_name} INTERFACE_INCLUDE_DIRECTORIES
    )
    if (_includes)
        foreach (item IN LISTS _includes)
            # if INTERFACE_INCLUDE_DIRECTORIES contains a generator expression, we only pick $<BUILD_INTERFACE:...>
            if (item MATCHES "^\\$<([A-Z_]+):(.+)>$")
                if (CMAKE_MATCH_1 STREQUAL "BUILD_INTERFACE")
                    set(TARGET_INCLUDES "${TARGET_INCLUDES} ${CMAKE_MATCH_2}")
                endif()
            else()
                set(TARGET_INCLUDES "${TARGET_INCLUDES} ${item}")
            endif()
        endforeach()
    endif()
endforeach()

set(doxyfile_in ${CMAKE_CURRENT_SOURCE_DIR}/Doxyfile.in)
set(doxyfile ${CMAKE_CURRENT_BINARY_DIR}/Doxyfile)

configure_file(${doxyfile_in} ${doxyfile} @ONLY)

add_custom_target(doxygen
    COMMAND ${DOXYGEN_EXECUTABLE} ${doxyfile}
    WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
    COMMENT "Generating API documentation with Doxygen"
    VERBATIM)
